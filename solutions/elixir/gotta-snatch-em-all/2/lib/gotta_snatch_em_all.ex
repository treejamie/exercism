defmodule GottaSnatchEmAll do
  @type card :: String.t()
  @type collection :: MapSet.t(card())

  @spec new_collection(card()) :: collection()
  def new_collection(card), do: MapSet.new([card])

  @spec add_card(card(), collection()) :: {boolean(), collection()}
  def add_card(card, collection) do
    {
      MapSet.member?(collection, card),
      MapSet.put(collection, card)
    }
  end

  @spec trade_card(card(), card(), collection()) :: {boolean(), collection()}
  def trade_card(your_card, their_card, collection) do
    # "got", "need"
    possible? =
      MapSet.member?(collection, your_card) and not MapSet.member?(collection, their_card)

    {
      possible?,
      collection
      |> MapSet.delete(your_card)
      |> MapSet.put(their_card)
    }
  end

  @spec remove_duplicates([card()]) :: [card()]
  def remove_duplicates(cards) do
    cards
    |> MapSet.new()
    |> MapSet.to_list()
    |> Enum.sort()
  end

  @spec extra_cards(collection(), collection()) :: non_neg_integer()
  def extra_cards(your_collection, their_collection) do
    MapSet.difference(your_collection, their_collection)
    |> MapSet.size()
  end

  @spec boring_cards([collection()]) :: [card()]
  def boring_cards([]), do: []

  def boring_cards(collections) do
    collections
    |> Enum.reduce(fn mapset, acc -> MapSet.intersection(acc, mapset) end)
    |> MapSet.to_list()
    |> Enum.sort()
  end

  @spec total_cards([collection()]) :: non_neg_integer()
  def total_cards([]), do: 0

  def total_cards(collections) do
    collections
    |> Enum.reduce(fn mapset, acc -> MapSet.union(acc, mapset) end)
    |> MapSet.size()
  end

  @spec split_shiny_cards(collection()) :: {[card()], [card()]}
  def split_shiny_cards(collection) do
    collection
    |> MapSet.split_with(&String.starts_with?(&1, "Shiny"))
    |> then(fn {shiny, other} ->
      {
        shiny |> MapSet.to_list() |> Enum.sort(),
        other |> MapSet.to_list() |> Enum.sort()
      }
    end)
  end
end
